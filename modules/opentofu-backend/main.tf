resource "aws_s3_bucket" "opentofu_state" {
  bucket = var.bucket_name
  
  lifecycle {
    prevent_destroy = true
  }
}

resource "aws_s3_bucket_versioning" "opentofu_bucket_versioning" {
  bucket = aws_s3_bucket.opentofu_state.id
  
  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "opentofu_config" {
  bucket = aws_s3_bucket.opentofu_state.id
  
  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

resource "aws_s3_bucket_public_access_block" "opentofu_access" {
  bucket = aws_s3_bucket.opentofu_state.id
  
  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

resource "aws_s3_bucket_lifecycle_configuration" "opentofu_lifestyle_config" {
  bucket = aws_s3_bucket.opentofu_state.id
  
  rule {
    id     = "delete-old-versions"
    status = "Enabled"
    
    filter {}
    
    noncurrent_version_expiration {
      noncurrent_days = 90
    }
  }
}
